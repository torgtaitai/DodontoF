<?xml version="1.0" encoding="UTF-8"?>
<mx:Application
   xmlns:mx="http://www.adobe.com/2006/mxml"
   xmlns:torgtaitai="*"
   pageTitle="{Language.s.title}"
   applicationComplete="setup()"
   layout="absolute"
   fontSize="{Config.fontSize}"
   >
  
  <mx:ApplicationControlBar 
     id="mainMenu"
     dock="true" horizontalGap="0"
     fillAlphas="[0.9, Config.windowAlpha]"
     enabled="false"
     >
    <mx:HBox id="mainMenuBody" width="100%" height="100%" visible="true">
      <mx:MenuBar id="mainMenuBar"
                  height="100%" 
                  dataProvider="{menuArray}" 
                  labelField="label"
                  itemClick="selectMenu(event)"
                  />
      <mx:Spacer width="20" />
      <mx:Image id="zoomInButton" source="{zoomInIcon}" click="dodontoF_Main.zoom(true)" />
      <mx:Image id="zoomOutButton" source="{zoomOutIcon}" click="dodontoF_Main.zoom(false)" />
      <mx:Spacer width="20" />
      
      <mx:Button id="displayPlayRoomInfoButton" label="RoomX:0" click="displayPlayRoomInfo()" enabled="false"/>
      
      <mx:Button id="sharedMemo"
                label="{Language.s.sharedMemo}"
                click="popup(AddMemoWindow, true)"
                 />
      
      <mx:Button id="openDrawWindow"
                 icon="{DrawMapWindow.pencilIcon}"
                 label="{Language.s.handwrite}"
                 click="DrawMapWindow.open(openDrawWindow)"
                 toggle="true"
                 />
      <mx:Spacer width="50%" />
      
      <mx:Button id="graveyard"
                 label="{Language.s.graveyardMenu}"
                 click="popup(GraveyardWindow, true)"
                 dragEnter="graveyardDragEnterHandler(event);" 
                 dragOver="graveyardDragOver(event)"
                 dragDrop="graveyardDragDropHandler(event)"
                 dragExit="graveyardDragExitHandler(event)"
                />
      
      <mx:Spacer width="50%" />
      
      <mx:Button icon="{cameraIcon}" click="captureScreen()"  paddingLeft="2" paddingRight="2" />
      <mx:Button id="mentenanceModeButton" label="MentenanceMode" click="changeMentenanceMode();" toggle="true" visible="false" width="0" />
      <mx:Button icon="{logoutIcon}" click="dodontoF_Main.logout()" label="{Language.s.logoutMenu}" />
      
    </mx:HBox>
    
    <mx:HBox id="speedBox" width="0" height="100%" visible="false">
      <mx:Label text="{Language.s.replayPosition}" />
      <mx:HSlider id="replaySeekSlider" 
                  showTrackHighlight="true"
                  width="100%"
                  minimum="0" maximum="100" value="0"
                  toolTip="{replaySeekSlider.value}"
                  change="DodontoF_Main.getInstance().changeReplayPoint(replaySeekSlider.value)"
                  />
      
      <mx:Spacer width="10" />
      <mx:Image id="pauseAndPlay" width="25" height="25" source="{pauseIcon}" click="DodontoF_Main.getInstance().pauseAndPlay()" />
      <mx:Spacer width="10" />
      
      <mx:Label text="{Language.s.replaySpeed}" />
      <mx:HSlider id="replaySpeedSlider" 
                  showTrackHighlight="true"
                  width="150"
                  minimum="0.1" maximum="10" value="1"
                  toolTip="{replaySpeedSlider.value}"
                  change="DodontoF_Main.getInstance().setReplaySpeed(replaySpeedSlider.value)"
                  labels="['0.1', '10']"
                  labelOffset="0"
                  />
      
      <mx:CheckBox id="isRepeat" label="{Language.s.repeat}" selected="false"
                   visible="false" width="0"
                   />

      <mx:Button icon="{cameraIcon}" click="captureScreen()"  paddingLeft="2" paddingRight="2" />
      <mx:Button icon="{logoutIcon}" click="dodontoF_Main.logoutFromReplay()" label="{Language.s.logoutFromReplay}" />
    </mx:HBox>
    
  </mx:ApplicationControlBar>
  
  <mx:Glow id="_glowEffect" duration="1500" 
           alphaFrom="1.0" alphaTo="0.0" 
           blurXFrom="70.0" blurXTo="0.0" 
           blurYFrom="70.0" blurYTo="0.0" 
           color="0xFFFF00"/>
  
  <torgtaitai:DodontoF_Main id="dodontoF_Main" x="0" y="0"/>
  <mx:Spacer id="diceRollPlace_" width="100%" height="100%" x="0" y="0"/>


  <mx:Script>
    <![CDATA[

    [Bindable]
    private var menuArray:Array;
    
    private var loginUserInfo:Array;
    
    import flash.net.FileReference;
    import mx.collections.ArrayCollection;
    import mx.containers.TitleWindow;
    import mx.controls.Alert;
    import mx.controls.ComboBox;
    import mx.controls.DataGrid;
    import mx.core.IFlexDisplayObject;
    import mx.core.UIComponent;
    import mx.events.CloseEvent;
    import mx.events.DragEvent;
    import mx.events.MenuEvent;
    import mx.managers.DragManager;
    import mx.managers.PopUpManager;
    import mx.binding.utils.BindingUtils;
    
    
    
    protected static var thisObj:DodontoF;
    
    [Embed(source='image/icons/zoom_in.png')]
    [Bindable]
    private static var zoomInIcon:Class;
    
    [Embed(source='image/icons/zoom_out.png')]
    [Bindable]
    private static var zoomOutIcon:Class;
    
    [Embed(source='image/icons/door_out.png')]
    [Bindable]
    private static var logoutIcon:Class;
    
    [Embed(source='image/icons/camera.png')]
    [Bindable]
    private static var cameraIcon:Class;
    
    [Embed(source='image/icons/control_pause_blue.png')]
    [Bindable]
    private static var pauseIcon:Class;
    
    [Embed(source='image/icons/control_pause.png')]
    [Bindable]
    private static var pauseDisableIcon:Class;

    [Embed(source='image/icons/control_play_blue.png')]
    [Bindable]
    private static var playIcon:Class;
    
    [Embed(source='image/icons/control_play.png')]
    [Bindable]
    private static var playDisableIcon:Class;
    
    
    private var standingGraphicLayer:UIComponent = new UIComponent();
    private var chatLogLayer:UIComponent = new UIComponent();
    
    public function getStandingGraphicLayer():UIComponent {
        return standingGraphicLayer;
    }
    
    public function setStandingGraphicVisible(visible:Boolean):void {
        standingGraphicLayer.visible = visible;
    }
    
    public function getGlowEffect():Glow {
        return _glowEffect;
    }
    
    private function initLayer():void {
        this.addChild(standingGraphicLayer);
        this.addChild(chatLogLayer);
        
        NovelticMode.addEventListenerOfMouseOverForChatLogLayer(chatLogLayer);
    }
    
    private function setup():void {
        thisObj = this;
        
        dodontoF_Main.setDodontoF(this);
        Language.setup();
        
        Log.loggingTuning("isMySqlMode()", dodontoF_Main.isMySqlMode());
        
        initForSetup();
    }
    
    private function initForSetup():void {
        menuArray = new Array();
        loginUserInfo = new Array();
        
        if( TEST::isTest ) {
            dodontoF_Main = new DodontoF_MainTest();
        }
        
        dodontoF_Main.init();
        
        initZoomRate();
        initToolTip();
        initLayer();
        setPauseIcon();
        initMenu();
        
        replaySeekSlider.snapInterval = 1;
        
        Map.setDodontoF(this);
        SharedDataReceiver.setDodontoF(this);
        MovablePiece.setDodontoF(this);
        
        start();
        
        setLogMode();
        Utils.setSkin(this);
        Config.getInstance().loadFontInfo();
    }
    
    private function setLogMode():void {
        if( dodontoF_Main.isErrorLogMode() ) {
            Log.initLogWindow();
            Log.setError();
            return;
        }
        
        if( dodontoF_Main.isTuningLogMode() ) {
            Log.initLogWindow();
            Log.setTuning();
            return;
        }
        
        if( dodontoF_Main.isDebugLogMode() ){
            Log.initLogWindow();
            Log.setDebug();
            return;
        }
    }
    
    private function initZoomRate():void {
        /*
        var zoomRate:Number = dodontoF_Main.getGlobalZoomRate();
        
        this.scaleX = zoomRate;
        this.scaleY = zoomRate;
        */
    }
    
    private function initToolTip():void {
        var fontSize:int = Config.fontSize;
        var maxWidth:int = 450;
        Utils.setToolTipStyle(fontSize, maxWidth);
    }
    
    private function start():void {
        dodontoF_Main.login();
    }
    
    public static function popup(className:Class, modal:Boolean = false):IFlexDisplayObject {
        modal = false;
        return popupForce(className, modal);
    }
    
    public static function popupForce(className:Class, modal:Boolean = false):IFlexDisplayObject {
        var window:IFlexDisplayObject = PopUpManager.createPopUp(thisObj, className, modal);
        return window;
    }
    
    public function startWindows():void {
        mainMenu.enabled = true;
        dodontoF_Main.setInitiativeWindow( popup(InitiativeWindow, false), "isInitiativeListVisible" );
        dodontoF_Main.setChatWindow( popup(ChatWindow, false), "isChatVisible");
        dodontoF_Main.setChatPaletteWindow( popup(ChatPalette, false), "isChatPaletteVisible" );
        dodontoF_Main.setDiceWindow( popup(DiceBox, false), "isDiceVisible" );
        dodontoF_Main.setCardPickUpWindow( popup(CardPickUpWindow, false), "isCardPickUpVisible" );
        dodontoF_Main.setButtonWindow( popup(CounterRemocon, false), "isCounterRemoconVisible" );
        dodontoF_Main.setResourceWindow( popup(ResourceWindow, false), "isResourceWindowVisible" );
        
    }
    
    public static function openUrl(urlString:String):void {
        var url:URLRequest = new URLRequest(urlString);
        navigateToURL(url);
    }
    
    public function selectMenu(event:MenuEvent):void {
        var menuName:String = event.item.data;
        var toggled:Boolean = false;
        if( event.item != null ) {
            toggled = event.item.toggled;
        }
        selectMenuByManuName(menuName, toggled);
    }
    
    public function selectMenuByManuName(menuName:String, toggled:Boolean):void {
        Log.logging("selectMenuByManuName menuName", menuName);
        
        if( menuName == "save" ) {
            popup(SaveWindow, true);
        } else if( menuName == "load" ) {
            popup(LoadWindow, true);
        } else if( menuName == "saveLog" ) {
            DodontoF_Main.getInstance().getChatWindow().saveChatLog();
        } else if( menuName == "startSessionRecording" ) {
            startSessionRecording();
        } else if( menuName == "stopSessionRecording" ) {
            stopSessionRecording();
        } else if( menuName == "cancelSessionRecording" ) {
            cancelSessionRecording();
        } else if( menuName == "saveAllData" ) {
            popup(SaveAllDataWindow, true);
        } else if( menuName == "loadAllSaveData" ) {
            dodontoF_Main.loadAllSaveData();
        } else if( menuName == "logout" ) {
            dodontoF_Main.logout();
        } else if( menuName == "initWindowPosition" ) {
            dodontoF_Main.initWindowPosition();
        } else if( menuName == "initLocalSaveData" ) {
            initLocalSaveData();
        } else if( menuName == "isChatVisible" ) {
            ChatWindow.getInstance().setVisibleState( toggled );
        } else if( menuName == "isDiceVisible" ) {
            dodontoF_Main.setDiceBoxVisible( toggled );
        } else if( menuName == "isChatPaletteVisible" ) {
            dodontoF_Main.setChatPaletteVisible( toggled );
        } else if( menuName == "isCounterRemoconVisible" ) {
            dodontoF_Main.setCounterRemoconVisible( toggled );
        } else if( menuName == "isResourceWindowVisible" ) {
            dodontoF_Main.setResourceWindowVisible( toggled );
        } else if( menuName == "isInitiativeListVisible" ) {
            InitiativeWindow.getInstance().setVisibleState( toggled );
        } else if( menuName == "isStandingGraphicVisible" ) {
            dodontoF_Main.getChatWindow().setStandingGraphicsDisplayState( toggled );
            Config.getInstance().saveViewStateInfo();
        } else if( menuName == "isCutInVisible" ) {
            Config.getInstance().setCutInDisplayState(toggled);
            Config.getInstance().saveViewStateInfo();
        } else if( menuName == "isMapVisible" ) {
            var visibleMap:Boolean = toggled;
            dodontoF_Main.getMap().setVisible( visibleMap );
            Config.getInstance().saveViewStateInfo();
        } else if( menuName == "isPositionVisible" ) {
            var visible:Boolean = toggled;
            dodontoF_Main.getMap().setVisibleGridPositionLayer( visible );
            Config.getInstance().saveViewStateInfo();
        } else if( menuName == "isGridVisible" ) {
            var visibleMapGrid:Boolean = toggled;
            dodontoF_Main.getMap().setVisibleGridLayer( visibleMapGrid );
            Config.getInstance().saveViewStateInfo();
        } else if( menuName == "isSnapMovablePiece" ) {
            var isSnapMovablePiece:Boolean = toggled;
            Config.getInstance().setSnapMovablePiece( isSnapMovablePiece );
        } else if( menuName == "isAdjustImageSize" ) {
            var isAdjustStandingGraphics:Boolean = toggled;
            Config.getInstance().setAdjustImageSizeMode(isAdjustStandingGraphics);
        } else if( menuName == "changeFontSize" ) {
            changeFontSize();
        } else if( menuName == "addCharacter" ) {
            var addCharacterWindow:AddCharacterWindow = popup(AddCharacterWindow, true) as AddCharacterWindow;
        } else if( menuName == "addMagicRange" ) {
            var addMagicRangeWindow:AddMagicRangeWindow = popup(AddMagicRangeWindow, true) as AddMagicRangeWindow;
            addMagicRangeWindow.setCharacterPosition();
        } else if( menuName == "addMagicRangeDD4th" ) {
            var addMagicRangeDD4thWindow:AddMagicRangeDD4thWindow
              = popup(AddMagicRangeDD4thWindow, true) as AddMagicRangeDD4thWindow;
            addMagicRangeDD4thWindow.setCharacterPosition();
        } else if( menuName == "addLogHorizonRange" ) {
            var addLogHorizonRangeWindow:AddLogHorizonRangeWindow
            = popup(AddLogHorizonRangeWindow, true) as AddLogHorizonRangeWindow;
            addLogHorizonRangeWindow.setPosition();
        } else if( menuName == "addMetallicGuardianDamageRange" ) {
            popup(AddMetallicGuardianDamageRangeWindow, true);
        } else if( menuName == "addMagicTimer" ) {
            popup(AddMagicTimerWindow, true);
        } else if( menuName == "graveyard" ) {
            popup(GraveyardWindow, true);
        } else if( menuName == "characterWaitingRoom" ) {
            if( ! CharacterWaitingRoomWindow.isOpened() ) {
                popup(CharacterWaitingRoomWindow, true);
            }
        } else if( menuName == "isRotateMarkerVisible" ) {
            var isRotateMarkerVisible:Boolean = toggled;
            Rotater.setGlobalVisible( isRotateMarkerVisible );
        } else if( menuName == "clearCards" ) {
            dodontoF_Main.clearCards();
        } else if( menuName == "isCardPickUpVisible" ) {
            dodontoF_Main.setCardPickUpVisible( toggled );
        } else if( menuName == "isCardHandleLogVisible" ) {
            dodontoF_Main.setCardHandleLogVisible( toggled );
        } else if( menuName == "openInitCardWindow" ) {
            popup(InitCardWindow, true);
        } else if( menuName == "cleanCard" ) {
            InitCardWindow.cleanCard();
        } else if( menuName == "changeMap" ) {
            popup(ChangeMapWindow, true);
        } else if( menuName == "createChit" ) {
            popup(AddChitWindow, true);
        } else if( menuName == "changeFloorTile" ) {
            popup(ChangeFloorTileWindow, true);
        } else if( menuName == "saveMap" ) {
            popup(SaveMapWindow, true);
        } else if( menuName == "loadMap" ) {
            popup(LoadMapWindow, true);
        } else if( menuName == "addMapMask" ) {
            popup(AddMapMaskWindow, false);
        } else if( menuName == "createMapEasy" ) {
            popup(CreateMapEasyWindow, true);
        } else if( menuName == "imageFileUploader" ) {
            openImageFileUploader();
        } else if( menuName == "webcameraCaptureUploader" ) {
            openWebcameraCaptureUploader();
        } else if( menuName == "deleteImage" ) {
            popup(ImageManageWindow, true);
            /*
              } else if( menuName == "movieFileUploader" ) {
              popup(MoviewUploadWindow, true);
              } else if( menuName == "deleteMovie" ) {
              popup(MovieManageWindow, true);
            */
        } else if( menuName == "openImageTagManager" ) {
            popup(TagManagerWindow, true);
        } else if( menuName == "version" ) {
            popup(VersionWindow, true);
        } else if( menuName == "manual" ) {
            openUrl("README.html");
        } else if( menuName == "tutorialReplay" ) {
            openUrl("http://www.dodontof.com/DodontoF/DodontoF.swf?replay=..%2fDownload%2ftutorial%2fTutorialOfDodontoF_20110116.rec");
        } else if( menuName == "officialSite" ) {
            openUrl("http://www.dodontof.com/");
        } else if( menuName == "initLogWindow" ) {
            Log.initLogWindow();
        } else if( menuName == "debugLog" ) {
            Log.setDebug();
        } else if( menuName == "tuningLog" ) {
            Log.setTuning();
        } else if( menuName == "errorLog" ) {
            Log.setError();
        } else if( menuName == "fatalErrorLog" ) {
            Log.setFatalError();
            /*
        } else if( menuName == "pass" ) {
            return;
            */
        } else {
            Log.loggingError("menuName is invalid", menuName);
        }
    }
    
    
    public function setPauseAndPlayIconDisable():void {
        pauseAndPlay.enabled = false;
        if( pauseAndPlay.source == pauseIcon ) {
            pauseAndPlay.source = pauseDisableIcon;
        }
        if( pauseAndPlay.source == playIcon ) {
            pauseAndPlay.source = playDisableIcon;
        }
    }
    
    public function setPlayIcon():void {
        pauseAndPlay.enabled = true;
        pauseAndPlay.source = playIcon;
        pauseAndPlay.toolTip = Language.s.replayButton;
    }
    
    public function setPauseIcon():void {
        pauseAndPlay.enabled = true;
        pauseAndPlay.source = pauseIcon;
        pauseAndPlay.toolTip = Language.s.pauseTips;
    }
    
    
    public function initLocalSaveData():void {
        var result:Alert = Alert.show(Language.s.isInitWindowPosition,
                                      Language.s.isInitWindowPositionWindow,
                                      (Alert.OK | Alert.CANCEL),
                                      null, 
                                      function(e : CloseEvent) : void {
                                          if (e.detail != Alert.OK) {
                                              return;
                                          }
                                          
                                          Config.getInstance().setToDefaultInfo();
                                          Alert.show(Language.s.initWindowPositionFinished);
                                          thisObj.dodontoF_Main.logoutExecute();
                                      });
    }
    
    public function openImageFileUploader(function_:Function = null):void {
        var window:ImageUploadWindow = popup(ImageUploadWindow, true) as ImageUploadWindow;
        window.setCloseEvent(function_);
    }

    public function openWebcameraCaptureUploader(function_:Function = null):void {
        var window:WebCameraCaptureWindow = popup(WebCameraCaptureWindow, true) as WebCameraCaptureWindow;
        window.setCloseEvent(function_);
    }
    
    public function getMainBarHeight():int {
        return mainMenuBar.height + 10;
    }
    public function getScreenWidth():int {
        return stage.stageWidth;
    }

    public function getScreenHeight():int {
        return stage.stageHeight;
    }
    
    private function checkChatVisible(event:Event):void {
        //    event.target.
    }
    
    
    public function setLoginUserInfo(loginUserInfoNew:Array):void {
        loginUserInfo = loginUserInfoNew;
        updateDisplayLoginUserInfoButtonLabel();
        updateChatWindowSendtoBox();
    }
    
    private function updateDisplayLoginUserInfoButtonLabel():void {
        var number:int =  DodontoF_Main.getInstance().getGuiInputSender().getRoomNumber();
        var roomNo:String = Language.text("roomNo", number);
        var roomDisplay:String = Language.text("roomDisplay", roomNo, loginUserInfo.length);
        displayPlayRoomInfoButton.label = roomDisplay;
        
        var playRoomName:String = DodontoF_Main.getInstance().getPlayRoomName();
        displayPlayRoomInfoButton.toolTip = playRoomName;
        displayPlayRoomInfoButton.enabled = true;
    }
    
    private function getSender():SharedDataSender {
        return dodontoF_Main.getGuiInputSender().getSender();
    }
    
    private function updateChatWindowSendtoBox():void {
        dodontoF_Main.getChatWindow().updateChatWindowSendtoBox( Utils.clone(loginUserInfo) );
    }
    
    public function getLoginUserCount(isIncludeVisiter:Boolean):int {
        if( isIncludeVisiter ) {
            return loginUserInfo.length;
        }
        
        return getLoginUserCountExcludeVisiter();
    }
    
    public function getLoginUserCountExcludeVisiter():int {
        var count:int = 0;
        
        for(var i:int = 0 ; i < loginUserInfo.length ; i++ ) {
            var userInfo:Object = loginUserInfo[i];
            
            if( userInfo.isVisiter ) {
                continue;
            }
            
            count++;
        }
        
        return count;
    }
    
    private function displayPlayRoomInfo():void {
        var window:DisplayPlayRoomInfo = popup(DisplayPlayRoomInfo, true) as DisplayPlayRoomInfo;
        
        var playRoomName:String = DodontoF_Main.getInstance().getPlayRoomName();
        var message:String = "";
        message += "【" + playRoomName + "】\n";
        message += "\n";
        message += Language.s.loginUserList + "\n";
        
        for(var i:int = 0 ; i < loginUserInfo.length ; i++ ) {
            var userInfo:Object = loginUserInfo[i];
            var uniqueId:String = getUniqueIdFromUserInfo(userInfo);
            message += Language.text("loginUserNameAndId", userInfo.userName, uniqueId);
            message += "\n";
        }
        
        window.init(message);
    }
    
    private function getUniqueIdFromUserInfo(userInfo:Object):String {
        var uniqueId:String = getSender().getUniqueIdFromStrictlyUniqueId(userInfo.userId);
        return uniqueId;
    }
    
    public function getUserNameByUniqueId(targetUniqueId:String):String {
        Log.logging("getUserNameByUniqueId Begin targetUniqueId", targetUniqueId);
        
        for(var i:int = 0 ; i < loginUserInfo.length ; i++ ) {
            var userInfo:Object = loginUserInfo[i];
            
            var uniqueId:String = userInfo.userId;
            Log.logging("check uniqueId", uniqueId);
            
            if( uniqueId == targetUniqueId ) {
                Log.logging("matched.");
                return userInfo.userName;
            }
        }
        
        Log.logging("uniqueId NOT found.");
        return "???";
    }
    
    public function getLoingUserCharacterList():Array {
        
        //チャットで選択しているキャラクター名は最優先でリストに載せるための処理。
        var ownInfo:Object = {
            userName: dodontoF_Main.getChatWindow().getChatCharacterName(),
            userId: getSender().getStrictlyUniqueId() };
        var userInfos:Object = Utils.clone(loginUserInfo);
        userInfos.push( ownInfo );
        
        
        var characters:Array = [];
        
        for(var i:int = 0 ; i < userInfos.length ; i++) {
            var userInfo:Object = userInfos[i];
            
            var character:Character = dodontoF_Main.getMap().findCharacterByName(userInfo.userName);
            
            if( character == null ) {
                continue;
            }
            
            if( isAlreadyExist(characters, character) ) {
                continue;
            }
            
            if( getSender().isOwnStrictlyUniqueId(userInfo.userId) ) {
                characters.splice(0, 0, character);
                continue;
            }
            
            characters.push(character);
        }
        
        return characters;
    }

    private function isAlreadyExist(array:Array, target:Object):Boolean {
        
        for(var i:int = 0 ; i < array.length ; i++) {
            var data:Object = array[i];
            if( data == target ) {
                return true;
            }
        }
        
        return false;
    }
    

    
    
    public function getOwnCharacter():Character {
        for(var i:int = 0 ; i < loginUserInfo.length ; i++) {
            var userInfo:Object = loginUserInfo[i];
            
            if( ! getSender().isOwnStrictlyUniqueId(userInfo.userId) ) {
                continue;
            }
            
            var character:Character = dodontoF_Main.getMap().findCharacterByName(userInfo.userName);
            if( character != null ) {
                return character;
            }
        }
        
        return null;
    }
    

    public function collectMainMenuInfos():Array {
        var result:Array = new Array();
        var array:Array = menuArray;
        
        return collectMenuInfos(array, result);
    }
    
    private function collectMenuInfos(array:Array, result:Array):Array {
        var targetTyep:String = "check";
        
        for(var i:int = 0 ; i < array.length ; i++) {
            var item:Object = array[i];
            if( item == null ) {
                continue;
            }
            
            if( item.type == targetTyep ) {
                result.push( item );
            }
            
            var children:Array = item.children;
            if( children != null ) {
                result = collectMenuInfos(children, result);
            }
        }
        
        return result;
    }
    
    public function findMainMenuItem(targetData:String, array:Array = null):Object {
        if( array == null ) {
            array = menuArray;
        }
        
        for(var i:int = 0 ; i < array.length ; i++) {
            var item:Object = array[i];
            if( item == null ) {
                continue;
            }
            
            if( item.data == targetData ) {
                return item;
            }
            
            var children:Array = item.children;
            if( children == null ) {
                continue;
            }
            
            var result:Object = findMainMenuItem(targetData, children);
            if( result != null ) {
                return result;
            }
        }
        
        return null;
    }
    
    public function getMainMenuToggle(key:String):Boolean {
        var result:Object = findMainMenuItem(key);
        if( result == null ) {
            return false;
        }
        
        return result.toggled
    }
    
    public function changeMainMenuToggle(targetData:String, value:Boolean):void {
        var result:Object = findMainMenuItem(targetData);
        if( result == null ) {
            Log.loggingError("targetData is invalid menu item data", targetData);
            return;
        }
        result.toggled = value;
        
        refreshMainMenu();
    }
    
    private function refreshMainMenu():void {
        var menuArrayLocal:Array = menuArray;
        menuArray = new Array();
        menuArray = menuArrayLocal;
    }
    
    public function initMenu():void {
        menuArray = dodontoF_Main.getMenuXml();
    }
    
    private function changeMentenanceMode():void {
        dodontoF_Main.setMentenanceModeOn( mentenanceModeButton.selected );
    }
    
    
    private function graveyardDragEnterHandler(event:DragEvent):void {
        var Initiator : * = event.currentTarget;
        DragManager.acceptDragDrop(Initiator);
    }
    
    private function graveyardDragOver(event:DragEvent):void {
        DragManager.showFeedback(DragManager.MOVE);
    }
    
    public function graveyardDragDropHandler(event:DragEvent):void {    
        var dragObj:Array = event.dragSource.dataForFormat("items") as Array; 
        var piece:InitiativedPiece = dragObj[0]["character"]
        piece.sendDelete();
    }
    
    private function graveyardDragExitHandler(event:DragEvent):void {
    }
    
    
    private function captureScreen():void {
        //Utils.saveCaptureImage( UIComponent(Application.application) );
        Utils.saveCaptureImage( this );
    }
    
    static public function changeFontSize():void {
           var window:ChangeAllFontWindow = DodontoF.popup(ChangeAllFontWindow, true) as ChangeAllFontWindow;
    }
    
    public function startSessionRecording(isResume:Boolean = false):void {
        setMenuSessionRecording(true);
        dodontoF_Main.startSessionRecording(isResume);
    }
    
    public function stopSessionRecording():void {
        dodontoF_Main.stopSessionRecording();
    }
    
    public function setMenuSessionRecording(isRecording:Boolean):void {
        findMainMenuItem("startSessionRecording").enabled = ( ! isRecording);
        findMainMenuItem("stopSessionRecording").enabled = isRecording;
        findMainMenuItem("cancelSessionRecording").enabled = isRecording;
        refreshMainMenu();
    }
    
    public function cancelSessionRecording():void {
        dodontoF_Main.cancelSessionRecording();
    }

    ]]>
  </mx:Script>
</mx:Application>


